//  /findBySnils. Поиск по СНИЛС. Упрощенная структура
begin(snils, status, qrPats,patient,err,ex) // dt,dtEhr,

    try 
    begin()

        @snils := if(@querystring["snils"]="") Null() else @querystring["snils"];
        @status := 200;

        //@patMain := new MP.PATIENTS_LIST();

        if (@snils <>Null())
        ////------------- ЕСЛИ УКАЗАН ПАРАМЕТР "snils" --------------
        begin()
            if (RegularExpression("^[0-9]{3}-[0-9]{3}-[0-9]{3} [0-9]{2}$", @snils) > 0 and @snils<>"000-000-000 00")
            //----- КОРРЕКТЕН ЛИ ФОРМАТ "snils" ------//
            begin()
                @qrPats := query top 1 {PatientID}
                                        ,{SNILS / Snils}
                                        ,{LastName}
                                        ,{FirstName}
                                        ,{FatherName}
                                        ,{Birthday}
                                        ,{Sex / ShortName}
                                        // ,{DocType / ShortName}
                                        // ,{DocSeries},{DocNumber}
                                        // ,{DocIssueDate}
                                        // ,{PolicyOmsNumber}
                                        // ,{AddressReal / DisplayName}
                                        // ,{AddressReal / FreeAddressString}
                                        // ,{PhoneNumbersString}
                from "AKUZ.PATIENT" where {SNILS / Snils} = @snils ;

                // ============== КРАТКИЙ ФОРМАТ  ==============
                // {
                //     "patId": "b62e9f22-a871-4c52-96d6-559c707a716d",
                //     "SNILS": "000-000-600 31",
                //     "lastName": "Тестовый",
                //     "firstName": "Пациент",
                //     "middleName": "Ребенок",
                //     "birthDate": "2020-10-16",
                //     "gender": "М"
                // }

                if (@qrPats->Count()>0 )  //----- НАЙДЕНЫ ЛИ ПАЦИЕНТЫ ------//
                foreach(thisPatnt in @qrPats) 
                begin()
                    @patient := new MP.PATIENT();
                    @patient->{patId} := @thisPatnt-> {PatientID};
                    @patient->{SNILS} := @thisPatnt-> {SNILS / Snils};
                    @patient->{lastName} := @thisPatnt->{LastName};
                    @patient->{firstName} := @thisPatnt->{FirstName};
                    if(@thisPatnt->{FatherName} <> Null()) @patient->{middleName} := @thisPatnt->{FatherName};
                    @patient->{birthDate} := Str(@thisPatnt->{Birthday},"yyyy-MM-dd");
                    @patient->{gender} := @thisPatnt->{Sex / ShortName}; 
                end
                else 
                begin()
                    @status:=206;
                    @err := new MP.ERROR();
                    @err -> {code} := "02";
                    @err-> {message} := "Не найден пациент по указанному СНИЛС";
                    @patient->{error} := @err;
                end;
            end
            else begin() //-- КРИВОЙ СНИЛС
                @status:=206;
                @err := new MP.ERROR();
                @err -> {code} := "01";
                @err-> {message} := "Параметр snils имеет неверный формат";
                @patient->{error} := @err;
            end;
        end else
        begin()
            @status:=206;
            @err := new MP.ERROR();
            @err -> {code} := "00";
            @err-> {message} := "Не указан параметр snils";
            @patient->{error} := @err;
        end;

        //if (@err <> Null()) return(@err) else return(@patMain);
        @response.StatusCode := @status;
        return(@patient);

    end
    catch(ex)
    begin(errJson)

        @err := @ex->{InnerException};
        // $throw(@err);

        while (RegExMatch("--> ", @err, "All")->Count()-1>0 )
             @err := SubString(@err,IndexOf(@err,"--> ")+4,StringLen(@err));

        @err := SubString(@err,IndexOf(@err,":")+2,IndexOf(@err,Char(13))-IndexOf(@err,":")-2 );

        @errJson := new MP.ERROR();
        @errJson -> {code} := "99";
        @errJson-> {message} := @err;

        @patient->{error} := @errJson;
        @response.StatusCode := 500;
        return(@patient);

    end;


end
